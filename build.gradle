plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id 'maven-publish'
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "com.modrinth.minotaur" version "2.+"
}

def ver = System.getenv("APPVEYOR_BUILD_VERSION")
if(ver == null)
	ver = "dev"
version = ver
group = 'com.troblecodings'
base {
	archivesName = 'TC-Redstone-1.15.2-Fabric'
}

sourceSets{
    main {
        java {
            srcDirs("linkableapi/src/")
        }
    }
}

def key = System.getenv("CURSETOKEN")
if(key == null)
	key = 'dev'
curseforge {
	apiKey = key
    project {
        id = '503143'
		changelog = file('changelog.md')
		changelogType = 'markdown'
		releaseType = 'release'
        addGameVersion 'Java 8'
		mainArtifact(remapJar) {
			displayName = "Example $project.version"
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

afterEvaluate {
    tasks.curseforge503143.dependsOn remapJar
}


def token = System.getenv("MODRINTHTOKEN")
if(key == null)
	key = 'dev'
modrinth {
    token = token
    projectId = 'rMlghquR'
    versionNumber = ver
	versionType = 'release'
    versionName = 'TC-Redstone-1.15.2-Fabric'
    uploadFile = remapJar
    gameVersions = ['1.15.2']
    loaders = ['fabric']
}

task publishToModSites {
    publishToModSites.dependsOn modrinth
    publishToModSites.dependsOn curseforge
}

dependencies {
	minecraft "com.mojang:minecraft:1.15.2"
	mappings "net.fabricmc:yarn:1.15.2+build.17:v2"
	modImplementation "net.fabricmc:fabric-loader:0.15.6"

	modImplementation "net.fabricmc.fabric-api:fabric-api:0.28.5+1.15"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 8
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
			maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
	}
}