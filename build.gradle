plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id 'maven-publish'
	id "com.matthewprenger.cursegradle" version "1.4.0"
}

def ver = System.getenv("APPVEYOR_BUILD_VERSION")
if(ver == null)
	ver = "dev"
version = ver
group = 'com.troblecodings'
base {
	archivesName = 'TC-Redstone-1.20.4-Fabric'
}

sourceSets{
    main {
        java {
            srcDirs("linkableapi/src/")
        }
    }
}

def key = System.getenv("CURSETOKEN")
if(key == null)
	key = 'dev'
curseforge {
	apiKey = key
    project {
        id = '503143'
		changelog = file('changelog.md')
		changelogType = 'markdown'
		releaseType = 'release'
        addGameVersion 'Java 17'
		mainArtifact(remapJar) {
			displayName = "Example $project.version"
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

afterEvaluate {
    tasks.curseforge503143.dependsOn remapJar
}

loom {
    splitEnvironmentSourceSets()

	mods {
		"tcredstone" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

}

dependencies {
	minecraft "com.mojang:minecraft:1.20.4"
	mappings "net.fabricmc:yarn:1.20.4+build.1:v2"
	modImplementation "net.fabricmc:fabric-loader:0.15.0"

	modImplementation "net.fabricmc.fabric-api:fabric-api:0.91.1+1.20.4"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
			maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
	}
}